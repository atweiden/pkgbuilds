# Maintainer: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: Cody P Schafer <aur@codyps.com>

pkgname=librevault-git
pkgver=20160811
pkgrel=1
pkgdesc="File synchronization designed with privacy in mind"
arch=('i686' 'x86_64')
depends=('boost'
         'boost-libs'
         'crypto++'
         'miniupnpc'
         'openssl'
         'protobuf3'
         'qt5-base'
         'qt5-svg'
         'qt5-tools'
         'qt5-websockets'
         'sqlite')
makedepends=('cmake' 'git' 'make')
url="https://github.com/Librevault/librevault"
license=('GPL3')
source=(git+https://github.com/Librevault/librevault
        git+https://github.com/Librevault/librevault.wiki
        common::git+https://github.com/Librevault/librevault-common
        dir_monitor::git+https://github.com/Librevault/dir_monitor
        spdlog::git+https://github.com/Librevault/spdlog
        docopt::git+https://github.com/Librevault/docopt.cpp
        rabin::git+https://github.com/Librevault/rabin
        libnatpmp::git+https://github.com/Librevault/libnatpmp
        websocketpp::git+https://github.com/Librevault/websocketpp
        dht::git+https://github.com/Librevault/dht
        miniupnp::git+https://github.com/Librevault/miniupnp)
sha256sums=('SKIP'
            'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP'
            'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP')
provides=('librevault')
conflicts=('librevault')

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

prepare() {
  cd ${pkgname%-git}

  msg2 'Fetching Git submodules...'
  git submodule init
  git config submodule.common.url "$srcdir/common"
  git config submodule.contrib/dir_monitor.url "$srcdir/dir_monitor"
  git config submodule.contrib/spdlog.url "$srcdir/spdlog"
  git config submodule.contrib/docopt.url "$srcdir/docopt"
  git config submodule.contrib/rabin.url "$srcdir/rabin"
  git config submodule.contrib/libnatpmp.url "$srcdir/libnatpmp"
  git config submodule.contrib/websocketpp.url "$srcdir/websocketpp"
  git config submodule.contrib/dht.url "$srcdir/dht"
  git config submodule.contrib/miniupnp.url "$srcdir/miniupnp"
  git submodule update
}

build() {
  cd ${pkgname%-git}

  msg2 'Building...'
  mkdir -p build && cd build
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=off \
    -DBUILD_UPDATER=off \
    ..
  cmake --build .
}

package() {
  cd ${pkgname%-git}

  msg2 'Installing license...'
  install -Dm 644 COPYING -t "$pkgdir/usr/share/licenses/${pkgname%-git}"

  msg2 'Installing documentation...'
  install -Dm 644 docs/* Readme.md -t "$pkgdir/usr/share/doc/${pkgname%-git}"

  msg2 'Installing wiki...'
  cp -dpr --no-preserve=ownership "$srcdir/${pkgname%-git}.wiki" \
    "$pkgdir/usr/share/doc/${pkgname%-git}/wiki"

  msg2 'Installing...'
  make DESTDIR="$pkgdir" install -C build

  msg2 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
