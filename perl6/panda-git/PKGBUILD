# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=panda-git
pkgver=20150719
pkgrel=1
pkgdesc="Module management solution for Rakudo Perl 6"
arch=('i686' 'x86_64')
depends=('rakudo')
makedepends=('git')
groups=('perl6')
url="https://github.com/tadzik/panda"
license=('MIT')
source=(git+https://github.com/tadzik/panda)
sha256sums=('SKIP')
provides=('panda')
conflicts=('panda')

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

build() {
  cd ${pkgname%-git}

  msg2 'Building...'
  perl6 bootstrap.pl
}

package() {
  cd ${pkgname%-git}

  msg2 'Installing...'
  install -dm 755 "$pkgdir/usr/share/panda"
  tar -c . | tar -x -C "$pkgdir/usr/share/panda"

  msg2 'Installing executables...'
  for _bin in panda \
              panda-build \
              panda-cpanize \
              panda-fetch \
              panda-install \
              panda-test \
              redpanda; do
    install -Dm 755 bin/$_bin "$pkgdir/usr/bin/$_bin"
  done

  msg2 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
