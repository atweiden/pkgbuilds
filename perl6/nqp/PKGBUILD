# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=nqp
pkgver=2016.03
pkgrel=1
pkgdesc="Lightweight Perl6-like environment for virtual machines, with MoarVM support"
arch=('i686' 'x86_64')
depends=('moarvm')
makedepends=('make' 'perl')
groups=('perl6')
url="https://github.com/perl6/nqp"
license=('PerlArtistic')
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/perl6/$pkgname/tar.gz/$pkgver)
sha256sums=('243dac4b4b05867152098a6727fa9accd713121bd33f6bc7ed7abe2802a50f98')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Building...'
  perl Configure.pl --prefix=/usr --backends=moar
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Installing...'
  make DESTDIR="$pkgdir" install
}
