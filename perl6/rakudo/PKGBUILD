# Maintainer: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: spider-mario <spidermario@free.fr>

pkgname=rakudo
pkgver=2015.12
pkgrel=1
pkgdesc="Perl 6 on MoarVM and the JVM"
arch=('i686' 'x86_64')
depends=('java-runtime' 'moarvm' 'nqp')
makedepends=('make' 'perl')
optdepends=('gdb: GDB runner'
            'valgrind: Valgrind runner')
groups=('perl6')
url="https://github.com/rakudo/rakudo"
license=('PerlArtistic')
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/rakudo/$pkgname/tar.gz/$pkgver)
sha256sums=('d7c4fd1c8bc525e16cefe2d28513dcd9642ecbed37775cd2a89b324a62278780')
provides=('perl6' 'rakudo')
conflicts=('perl6' 'rakudo')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Building...'
  perl Configure.pl --prefix=/usr --backends=moar,jvm
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Installing...'
  make DESTDIR="$pkgdir" install

  msg2 'Fixing precomp references...'
  find "$pkgdir/usr/share/perl6/precomp" -type f -exec \
    env _pkgdir="$pkgdir" perl -pe 's/\Q$ENV{_pkgdir}//g' -i '{}' '+'
}
