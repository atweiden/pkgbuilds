# Maintainer: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: spider-mario <spidermario@free.fr>
# JVM build disabled 2016-02-02 "Missing serialize function for REPR ContextRef"

pkgname=rakudo
pkgver=2016.01.1
pkgrel=1
pkgdesc="Perl6 on MoarVM"
arch=('i686' 'x86_64')
depends=('moarvm' 'nqp')
makedepends=('make' 'perl')
optdepends=('gdb: GDB runner'
            'valgrind: Valgrind runner')
groups=('perl6')
url="https://github.com/rakudo/rakudo"
license=('PerlArtistic')
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/rakudo/$pkgname/tar.gz/$pkgver)
sha256sums=('e3ebd74cd4476311801d9069b8b83018ccf635ac9a53e2593a398fdce583a462')
provides=('perl6' 'rakudo')
conflicts=('perl6' 'rakudo')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Building...'
  perl Configure.pl --prefix=/usr --backends=moar
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Installing...'
  RAKUDO_LOG_PRECOMP=1 make DESTDIR="$pkgdir" install

  msg2 'Cleaning up pkgdir...'
  find "$pkgdir" -type f -name "*.lock" -exec rm '{}' \;
  find "$pkgdir" -type f -print0 | xargs -0 sed -i "s,$pkgdir,,g"
}
