# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=rakudo
pkgver=2015.11
pkgrel=1
pkgdesc="Perl 6 on MoarVM and the JVM"
arch=('i686' 'x86_64')
depends=('java-runtime' 'moarvm' 'nqp')
makedepends=('make')
optdepends=('gdb: GDB runner'
            'valgrind: Valgrind runner')
groups=('perl6')
url="https://github.com/rakudo/rakudo"
license=('PerlArtistic')
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/rakudo/$pkgname/tar.gz/$pkgver)
sha256sums=('ae44cf3e2074671e862046375d6bb3c076ae0e82e06c8ef58588c2b7750b6f86')
provides=('perl6' 'rakudo')
conflicts=('perl6' 'rakudo')

build() {
  cd "$srcdir/$pkgname-${pkgver#v}"

  msg2 'Building...'
  perl Configure.pl --prefix=/usr --backends=moar,jvm
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver#v}"

  msg2 'Installing...'
  make DESTDIR="$pkgdir" install
}
