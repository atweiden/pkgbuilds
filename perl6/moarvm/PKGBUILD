# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=moarvm
pkgver=2015.09
pkgrel=1
pkgdesc="6model-based VM for NQP and Rakudo Perl 6"
arch=('i686' 'x86_64')
makedepends=('clang' 'git' 'llvm' 'make' 'pkg-config')
groups=('perl6')
url="https://github.com/MoarVM/MoarVM"
license=('PerlArtistic')
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/MoarVM/MoarVM/tar.gz/$pkgver
        dynasm::git+https://github.com/MoarVM/dynasm
        dyncall::git+https://github.com/MoarVM/dyncall
        libuv::git+https://github.com/libuv/libuv)
sha256sums=('8f7551404a0669beabce99cb1980062db40c691f514617b12224b46edfeaf79b'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd "$srcdir/MoarVM-$pkgver"

  msg2 'Copying Git submodules...'
  for _submodule in dynasm dyncall libuv; do
    rm -rf 3rdparty/$_submodule
    cp -dpr --no-preserve=owner "$srcdir/$_submodule" 3rdparty
  done
}

build() {
  cd "$srcdir/MoarVM-$pkgver"

  msg2 'Building...'
  perl Configure.pl --prefix=/usr \
                    --toolchain=gnu \
                    --compiler=clang \
                    --optimize
  make
}

package() {
  cd "$srcdir/MoarVM-$pkgver"

  msg2 'Installing...'
  make DESTDIR="$pkgdir" install
}
