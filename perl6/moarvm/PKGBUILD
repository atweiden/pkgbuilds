# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=moarvm
pkgver=2016.04
pkgrel=1
pkgdesc="6model-based VM for NQP and Rakudo Perl6"
arch=('armv6h' 'armv7h' 'i686' 'x86_64')
depends=('dyncall' 'libatomic_ops' 'libtommath' 'libuv' 'llvm')
makedepends=('clang' 'git' 'make' 'perl' 'pkg-config')
groups=('perl6')
url="https://github.com/MoarVM/MoarVM"
license=('PerlArtistic')
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/MoarVM/MoarVM/tar.gz/$pkgver
        dynasm::git+https://github.com/MoarVM/dynasm)
sha256sums=('ab4a74b20c25e41a48ee945930296270e5690ab286ff7cd8dfa4eaba7b3972f6'
            'SKIP')

prepare() {
  cd "$srcdir/MoarVM-$pkgver"

  msg2 'Copying Git submodules...'
  for _submodule in dynasm; do
    rm -rf 3rdparty/$_submodule
    cp -dpr --no-preserve=owner "$srcdir/$_submodule" 3rdparty
  done
}

build() {
  cd "$srcdir/MoarVM-$pkgver"

  msg2 'Building...'
  perl Configure.pl \
    --prefix=/usr \
    --toolchain=gnu \
    --compiler=clang \
    --has-dyncall \
    --has-libatomic_ops \
    --has-libtommath \
    --has-libuv \
    --optimize
  make
}

package() {
  cd "$srcdir/MoarVM-$pkgver"

  msg2 'Installing...'
  make DESTDIR="$pkgdir" install

  msg2 'Cleaning up pkgdir...'
  rm -rf "$pkgdir/usr/include/libuv"
}
