# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=moarvm
pkgver=2017.03
pkgrel=1
pkgdesc="6model-based VM for NQP and Rakudo Perl6"
arch=('armv6h' 'armv7h' 'i686' 'x86_64')
depends=('dyncall' 'libatomic_ops' 'libffi' 'libuv' 'llvm')
makedepends=('binutils' 'clang' 'git' 'make' 'perl' 'pkg-config')
groups=('perl6')
url="https://github.com/MoarVM/MoarVM"
license=('PerlArtistic')
options=('!purge')
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/MoarVM/MoarVM/tar.gz/$pkgver
        git+https://github.com/MoarVM/dynasm
        git+https://github.com/MoarVM/libtommath)
sha256sums=('70c8420d9599cc5297a41a515424132ce883cd91f8f739f554a267424a911716'
            'SKIP'
            'SKIP')

prepare() {
  cd "$srcdir/MoarVM-$pkgver"

  msg2 'Copying Git submodules...'
  for _submodule in dynasm libtommath; do
    rm -rf 3rdparty/$_submodule
    cp -dpr --no-preserve=owner "$srcdir/$_submodule" 3rdparty
  done
}

build() {
  cd "$srcdir/MoarVM-$pkgver"

  msg2 'Building...'
  perl Configure.pl \
    --prefix=/usr \
    --toolchain=gnu \
    --compiler=clang \
    --has-dyncall \
    --has-libatomic_ops \
    --has-libffi \
    --has-libuv \
    --optimize
  make
}

package() {
  cd "$srcdir/MoarVM-$pkgver"

  msg2 'Installing documentation...'
  install -Dm 644 README.markdown -t "$pkgdir/usr/share/doc/$pkgname"
  cp -dpr --no-preserve=ownership docs "$pkgdir/usr/share/doc/$pkgname/design"

  msg2 'Installing license...'
  install -Dm 644 Artistic2.txt CREDITS LICENSE \
          -t "$pkgdir/usr/share/licenses/$pkgname"

  msg2 'Installing...'
  make DESTDIR="$pkgdir" install
}
