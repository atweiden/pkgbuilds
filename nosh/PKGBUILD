# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=nosh
pkgver=1.29
pkgrel=1
pkgdesc="Suite of system-level utilities for initializing and running a BSD or Linux system, and for managing daemons - a superset of Bernstein's daemontools mechanism"
arch=('i686' 'x86_64')
depends=('ncurses')
makedepends=('docbook-xsl' 'pax' 'redo' 'xmlto')
url="https://jdebp.eu/Softwares/nosh"
license=('custom:ISC')
source=(https://jdebp.eu/Repository/freebsd/$pkgname-$pkgver.tar.gz)
sha256sums=('70f9281e008fa4b54fb96921c30b41d28d2f5b63464a46ce371214de472376c3')

# - android-tools owns /usr/bin/fastboot
# - coreutils owns /usr/bin/{chroot,true,false}
# - kbd owns /usr/bin/resizecons
# - ncurses owns /usr/bin/reset
# - the util-linux package owns /usr/bin/setsid /usr/bin/unshare
conflicts=('android-tools'
           'coreutils'
           'kbd'
           'ncurses'
           'systemd'
           'systemd-sysvcompat'
           'util-linux')

prepare() {
  cd "$srcdir"

  msg2 'Fixing ncurses header imports...'
  find . -type f -print0 | xargs -0 sed -i 's@ncursesw/curses\.h@curses.h@g'
}

build() {
  cd "$srcdir"

  msg2 'Building...'
  ./package/compile
}

package() {
  cd "$srcdir"

  msg2 'Installing license...'
  install -Dm 644 source/COPYING -t "$pkgdir/usr/share/licenses/nosh"

  msg2 'Installing...'
  ./package/export "$pkgdir/usr/" "$pkgdir/" "$pkgdir/var/"
}
