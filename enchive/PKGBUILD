# Maintainer: Andy Weidenbaum <atweiden@tutanota.de>

pkgname='enchive'
pkgver=3.5
pkgrel=1
pkgdesc='Encrypted personal archives'
arch=('aarch64'
      'armv6h'
      'armv7h'
      'i686'
      'x86_64')
makedepends=('make')
url='https://github.com/skeeto/enchive'
license=('Unlicense')
source=("https://github.com/skeeto/enchive/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('cb867961149116443a85d3a64ef5963e3c399bdd377b326669bb566a3453bd06'
            'SKIP')
validpgpkeys=('5EEB8C8D5069C4E9B94AA852AFD1503A8C8FF42A')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 "Building..."
  CFLAGS='-ansi -pedantic -Wall -Wextra -O3 -g3 -D_FILE_OFFSET_BITS=64'
  CFLAGS="$CFLAGS -DENCHIVE_AGENT_TIMEOUT=420"
  CFLAGS="$CFLAGS -DENCHIVE_FILE_EXTENSION='.enc'"
  make CFLAGS="$CFLAGS"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Installing license...'
  install -Dm 644 UNLICENSE \
          -t "$pkgdir/usr/share/licenses/$pkgname"

  msg2 'Installing documentation...'
  install -Dm 644 *.md \
          -t "$pkgdir/usr/share/doc/$pkgname"

  msg2 'Installing...'
  make \
    DESTDIR="$pkgdir" \
    PREFIX='/usr' \
    install
}
