# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname='moarvm'
pkgver=2020.05
pkgrel=1
pkgdesc='VM with adaptive optimization and JIT compilation, built for Rakudo'
arch=('aarch64'
      'armv6h'
      'armv7h'
      'i686'
      'x86_64')
depends=('libatomic_ops'
         'libffi'
         'libtommath'
         'libuv'
         'zstd')
makedepends=('binutils'
             'git'
             'make'
             'perl'
             'pkgconf')
groups=('raku')
url='https://moarvm.org'
license=('PerlArtistic')
options=('!purge')
source=("https://moarvm.org/releases/MoarVM-$pkgver.tar.gz"{,.asc})
sha256sums=('9368d2c691ea8710459a48c323915df6648c4a67d2ce24c27fc74f6b084a824b'
            'SKIP')
validpgpkeys=('E2AEB3D93FB3940B844C9E08A68DF012C3881D62')

build() {
  cd "$srcdir/MoarVM-$pkgver"

  msg2 'Building...'
  perl Configure.pl \
    --has-libatomic_ops \
    --has-libffi \
    --has-libtommath \
    --has-libuv \
    --optimize \
    --pkgconfig='/usr/bin/pkgconf' \
    --prefix='/usr' \
    --toolchain='gnu'
  make
}

package() {
  cd "$srcdir/MoarVM-$pkgver"

  msg2 'Installing documentation...'
  install -Dm 644 README.markdown \
          -t "$pkgdir/usr/share/doc/$pkgname"
  cp -dpr --no-preserve='ownership' docs \
    "$pkgdir/usr/share/doc/$pkgname/design"

  msg2 'Installing license...'
  install -Dm 644 Artistic2.txt \
                  CREDITS \
                  LICENSE \
          -t "$pkgdir/usr/share/licenses/$pkgname"

  msg2 'Installing...'
  make \
    DESTDIR="$pkgdir" \
    install
}
