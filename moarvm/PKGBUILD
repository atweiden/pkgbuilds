# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=moarvm
pkgver=2019.11
pkgrel=1
pkgdesc="VM for NQP and Rakudo Perl 6"
arch=('aarch64' 'armv6h' 'armv7h' 'i686' 'x86_64')
depends=('libatomic_ops' 'libffi' 'libuv' 'zstd')
makedepends=('binutils' 'git' 'make' 'perl' 'pkgconf')
groups=('perl6')
url="https://github.com/MoarVM/MoarVM"
license=('PerlArtistic')
options=('!purge')
source=(https://moarvm.com/releases/MoarVM-$pkgver.tar.gz{,.asc})
sha256sums=('d882c5114e35800eba2226f2104997052e98b3efdea6463e7edb213a87870201'
            'SKIP')
validpgpkeys=('E2AEB3D93FB3940B844C9E08A68DF012C3881D62')

build() {
  cd "$srcdir/MoarVM-$pkgver"

  msg2 'Building...'
  perl Configure.pl \
    --has-libatomic_ops \
    --has-libffi \
    --has-libuv \
    --optimize \
    --pkgconfig=/usr/bin/pkgconf \
    --prefix=/usr \
    --toolchain=gnu
  make
}

package() {
  cd "$srcdir/MoarVM-$pkgver"

  msg2 'Installing documentation...'
  install -Dm 644 README.markdown -t "$pkgdir/usr/share/doc/$pkgname"
  cp -dpr --no-preserve=ownership docs "$pkgdir/usr/share/doc/$pkgname/design"

  msg2 'Installing license...'
  install -Dm 644 Artistic2.txt CREDITS LICENSE \
          -t "$pkgdir/usr/share/licenses/$pkgname"

  msg2 'Installing...'
  make DESTDIR="$pkgdir" install
}
