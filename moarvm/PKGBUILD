# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=moarvm
pkgver=2019.07.1
pkgrel=1
pkgdesc="6model-based VM for NQP and Rakudo Perl6"
arch=('aarch64' 'armv6h' 'armv7h' 'i686' 'x86_64')
# XXX: next release, depends+=zstd
# https://github.com/MoarVM/MoarVM/commit/e417bb5088fef51c339b0b30769cec1eb4b7ab8f
depends=('libatomic_ops' 'libffi' 'libtommath' 'libuv')
makedepends=('binutils' 'clang' 'git' 'make' 'perl' 'pkg-config')
groups=('perl6')
url="https://github.com/MoarVM/MoarVM"
license=('PerlArtistic')
options=('!purge')
source=(https://moarvm.com/releases/MoarVM-$pkgver.tar.gz{,.asc})
sha256sums=('253c945ee6b589ede6949b8e77814542befcbc326190e18ef549634db4955988'
            'SKIP')
validpgpkeys=('E2AEB3D93FB3940B844C9E08A68DF012C3881D62')

build() {
  cd "$srcdir/MoarVM-$pkgver"

  msg2 'Building...'
  perl Configure.pl \
    --prefix=/usr \
    --toolchain=gnu \
    --compiler=clang \
    --has-libatomic_ops \
    --has-libffi \
    --has-libtommath \
    --has-libuv \
    --optimize
  make
}

package() {
  cd "$srcdir/MoarVM-$pkgver"

  msg2 'Installing documentation...'
  install -Dm 644 README.markdown -t "$pkgdir/usr/share/doc/$pkgname"
  cp -dpr --no-preserve=ownership docs "$pkgdir/usr/share/doc/$pkgname/design"

  msg2 'Installing license...'
  install -Dm 644 Artistic2.txt CREDITS LICENSE \
          -t "$pkgdir/usr/share/licenses/$pkgname"

  msg2 'Installing...'
  make DESTDIR="$pkgdir" install
}
