# Maintainer: Andy Weidenbaum <atweiden@tutanota.de>

pkgname='moarvm'
pkgver=2022.12
pkgrel=1
pkgdesc='VM with adaptive optimization and JIT compilation, built for Rakudo'
arch=('aarch64'
      'armv6h'
      'armv7h'
      'i686'
      'x86_64')
depends=('libffi'
         'libtommath'
         'libuv'
         'zstd')
makedepends=('binutils'
             'git'
             'make'
             'perl'
             'pkgconf')
groups=('raku')
url='https://moarvm.org'
license=('PerlArtistic')
options=('!purge')
source=("https://moarvm.org/releases/MoarVM-$pkgver.tar.gz"{,.asc})
sha256sums=('51c3e9c9a7a191c148f213b65ae1f4fcfe5d4b7c16c86300e9ee8e18eaa8becb'
            'SKIP')
# https://keys.openpgp.org/vks/v1/by-fingerprint/59E634736AFDCF9C6DBAC382602D51EACA887C01
validpgpkeys=('59E634736AFDCF9C6DBAC382602D51EACA887C01')

build() {
  cd "$srcdir/MoarVM-$pkgver"

  msg2 'Building...'
  perl Configure.pl \
    --c11-atomics \
    --has-libffi \
    --has-libtommath \
    --has-libuv \
    --optimize \
    --pkgconfig='/usr/bin/pkgconf' \
    --prefix='/usr' \
    --toolchain='gnu'
  make
}

package() {
  cd "$srcdir/MoarVM-$pkgver"

  msg2 'Installing documentation...'
  install -Dm 644 README.markdown \
          -t "$pkgdir/usr/share/doc/$pkgname"

  msg2 'Installing license...'
  install -Dm 644 Artistic2.txt \
                  CREDITS \
                  LICENSE \
          -t "$pkgdir/usr/share/licenses/$pkgname"

  msg2 'Installing...'
  make \
    DESTDIR="$pkgdir" \
    install
}
