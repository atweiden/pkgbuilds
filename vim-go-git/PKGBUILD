# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=vim-go-git
pkgver=20140903
pkgrel=1
pkgdesc="Go development plugin for Vim"
arch=('any')
depends=('vim')
makedepends=('git')
optdepends=('git: install go binaries on startup. To disable this behaviour add let g:go_disable_autoinstall = 1'
            'mercurial: install go binaries on startup. To disable this behaviour add let g:go_disable_autoinstall = 1'
            'vim-neocomplete-git: real-time code completion'
            'vim-neosnippet: code snippets'
            'vim-tagbar: display source code tag information in sidebar'
            'vim-ultisnips: code snippets'
            'vim-youcompleteme-git: real-time code completion')
groups=('vim-plugins')
url="https://github.com/fatih/vim-go"
license=('custom:vim')
source=(git+https://github.com/fatih/vim-go)
sha256sums=('SKIP')
provides=('vim-go')
conflicts=('vim-go')
install=vimdoc.install

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

package() {
  cd ${pkgname%-git}

  msg 'Installing docs...'
  install -Dm 644 README.md "$pkgdir/usr/share/doc/vim-go/README.md"

  msg 'Installing dirs...'
  install -dm 755 "$pkgdir/usr/share/vim/vimfiles"
  for _dir in autoload compiler doc ftdetect ftplugin gosnippets indent plugin syntax; do
    cp -dpr --no-preserve=ownership $_dir "$pkgdir/usr/share/vim/vimfiles/$_dir"
  done

  msg 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
