# Maintainer: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: Dmitry Korzhevin <dkorzhevin AT gmail DOT com>
# Contributor: C.Coutinho <kikijump[at]gmail[dot]com>
# Contributor: Grigorios Bouzakis <grbzks[at]gmail[dot]com>
# Contributor: TDY <tdy@gmx.com>

pkgname=tmux-git
pkgver=20151022
pkgrel=1
pkgdesc="A terminal multiplexer with select-pane -LDUR support patch"
arch=('i686' 'x86_64')
depends=('libevent' 'libutempter' 'ncurses')
makedepends=('git' 'make' 'pkg-config')
url="https://tmux.github.io"
license=('BSD')
source=(git+https://github.com/tmux/tmux)
sha256sums=('SKIP')
provides=('tmux')
conflicts=('tmux')

pkgver() {
  cd ${pkgname%%-*}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

build() {
  cd ${pkgname%%-*}

  msg2 'Building...'
  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/tmux \
    --sysconfdir=/etc \
    --sharedstatedir=/usr/share/tmux \
    --localstatedir=/var/lib/tmux
  make
}

package() {
  cd ${pkgname%%-*}

  msg2 'Installing...'
  make DESTDIR="$pkgdir" install

  msg2 'Installing license...'
  install -Dm 644 "COPYING" "$pkgdir/usr/share/licenses/tmux/COPYING"

  msg2 'Installing documentation...'
  install -dm 755 "$pkgdir/usr/share/doc/tmux/"
  install -m 644 examples/* "$pkgdir/usr/share/doc/tmux/"

  msg2 'Installing Bash completions...'
  install -Dm 644 examples/bash_completion_tmux.sh \
    "$pkgdir/usr/share/bash-completion/completions/tmux"

  msg2 'Installing Vim syntax...'
  install -Dm 644 examples/tmux.vim \
    "$pkgdir/usr/share/vim/vimfiles/syntax/tmux.vim"

  msg2 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
