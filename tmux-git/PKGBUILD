# Maintainer: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: Dmitry Korzhevin <dkorzhevin AT gmail DOT com>
# Contributor: C.Coutinho <kikijump[at]gmail[dot]com>
# Contributor: Grigorios Bouzakis <grbzks[at]gmail[dot]com>
# Contributor: TDY <tdy@gmx.com>

pkgname=tmux-git
pkgver=20160326
pkgrel=1
pkgdesc="A terminal multiplexer"
arch=('i686' 'x86_64')
depends=('libevent' 'libutempter' 'ncurses')
makedepends=('autoconf' 'automake' 'git' 'make' 'pkg-config')
url="https://tmux.github.io"
license=('BSD')
source=(git+https://github.com/tmux/tmux
        bash_completion_tmux.sh::https://raw.githubusercontent.com/tmux/tmux/8cf1504ba67307ee885c19c12d63a6a22016a582/examples/bash_completion_tmux.sh)
sha256sums=('SKIP'
            'f1c8650e2351d7ab92e3a0d280281160975b3dbd8c1f0c5d801f2356b7c2141d')
provides=('tmux')
conflicts=('tmux')

pkgver() {
  cd ${pkgname%%-*}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

build() {
  cd ${pkgname%%-*}

  msg2 'Building...'
  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/tmux \
    --sysconfdir=/etc \
    --sharedstatedir=/usr/share/tmux \
    --localstatedir=/var/lib/tmux
  make
}

package() {
  cd ${pkgname%%-*}

  msg2 'Installing...'
  make DESTDIR="$pkgdir" install

  msg2 'Installing license...'
  install -Dm 644 "COPYING" -t "$pkgdir/usr/share/licenses/tmux"

  msg2 'Installing documentation...'
  install -Dm 644 CHANGES \
                  FAQ \
                  README \
                  SYNCING \
                  TODO \
                  example_tmux.conf \
          -t "$pkgdir/usr/share/doc/tmux"

  msg2 'Installing Bash completions...'
  install -Dm 644 "$srcdir/bash_completion_tmux.sh" \
    "$pkgdir/usr/share/bash-completion/completions/tmux"

  msg2 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}
