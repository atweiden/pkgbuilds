# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=cryfs-git
pkgver=20180204
pkgrel=1
pkgdesc="Cryptographic filesystem for the cloud"
arch=('aarch64' 'armv6h' 'armv7h' 'i686' 'x86_64')
depends=('boost'
         'boost-libs'
         'crypto++'
         'curl'
         'fuse2'
         'openssl')
makedepends=('cmake' 'git' 'make' 'python')
url="https://www.cryfs.org"
license=('LGPL3')
source=(git+https://github.com/cryfs/cryfs
        git+https://github.com/cryfs/cryfs.wiki)
sha256sums=('SKIP' 'SKIP')
provides=('cryfs')
conflicts=('cryfs')

# half of available processing units or one if only one is available
_nproc=$(($(nproc)/2))
[[ ${_nproc} < 1 ]] && _nproc=1

pkgver() {
  cd ${pkgname%-git}
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

build() {
  cd ${pkgname%-git}

  msg2 'Building...'
  mkdir cmake && cd cmake
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=off \
    -DCRYFS_UPDATE_CHECKS=off \
    ..
  make -j$_nproc
}

package() {
  cd ${pkgname%-git}

  msg2 'Installing license...'
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/${pkgname%-git}"

  msg2 'Installing documentation...'
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/${pkgname%-git}"

  msg2 'Installing wiki...'
  cp -dpr --no-preserve=ownership "$srcdir/${pkgname%-git}.wiki" \
    "$pkgdir/usr/share/doc/${pkgname%-git}/wiki"

  msg2 'Installing...'
  make DESTDIR="$pkgdir" install -C cmake

  msg2 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
