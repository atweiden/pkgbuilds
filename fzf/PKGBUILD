# Maintainer: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: foalsrock <foalsrock at gmail dot-com>
# Contributor: jebaum <jebaum at ucla dot edu>

pkgname=fzf
pkgver=0.18.0
pkgrel=1
pkgdesc="Command-line fuzzy finder"
arch=('aarch64' 'armv6h' 'armv7h' 'i686' 'x86_64')
depends=('bash')
makedepends=('git' 'go' 'make')
optdepends=('fish: fish keybindings'
            'fzf-extras: fzf keybindings from junegunn wiki and dotfiles'
            'tmux: fzf-tmux script for launching fzf in a tmux pane'
            'vim-fzf: vim plugin'
            'zsh: zsh keybindings')
url="https://github.com/junegunn/fzf"
license=('MIT')
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/junegunn/$pkgname/tar.gz/$pkgver
        git+https://github.com/junegunn/$pkgname.wiki)
sha256sums=('5406d181785ea17b007544082b972ae004b62fb19cdb41f25e265ea3cc8c2d9d'
            'SKIP')
install=fzf.install

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Building...'
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Installing documentation...'
  for _doc in README.md install uninstall; do
    install -Dm 644 "$_doc" -t "$pkgdir/usr/share/doc/fzf"
  done

  msg2 'Installing wiki...'
  cp -dpr --no-preserve=ownership "$srcdir/fzf.wiki" \
    "$pkgdir/usr/share/doc/fzf/wiki"

  msg2 'Installing man pages...'
  install -Dm 644 man/man1/* -t "$pkgdir/usr/share/man/man1"

  msg2 'Installing executables...'
  find target -mindepth 1 -maxdepth 1 -type f -exec \
    install -Dm 755 '{}' "$pkgdir/usr/bin/fzf" \;
  install -Dm 755 "bin/fzf-tmux" -t "$pkgdir/usr/bin"

  msg2 'Installing bash completion and keybindings...'
  install -Dm 644 "shell/completion.bash" \
    "$pkgdir/usr/share/bash-completion/completions/fzf"
  install -Dm 755 "shell/key-bindings.bash" "$pkgdir/etc/profile.d/fzf.bash"

  msg2 'Installing fish keybindings...'
  install -Dm 644 "shell/key-bindings.fish" \
    "$pkgdir/usr/share/fish/functions/fzf.fish"

  msg2 'Installing zsh completion and keybindings...'
  install -Dm 644 "shell/completion.zsh" \
    "$pkgdir/usr/share/zsh/site-functions/_fzf"
  install -Dm 755 "shell/key-bindings.zsh" "$pkgdir/etc/profile.d/fzf.zsh"

  msg2 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
