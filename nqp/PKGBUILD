# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname='nqp'
pkgver=2020.01
pkgrel=1
pkgdesc='Lightweight Raku-like environment for virtual machines'
arch=('aarch64'
      'armv6h'
      'armv7h'
      'i686'
      'x86_64')
depends=('libatomic_ops'
         'libffi'
         'libuv'
         'moarvm')
makedepends=('make'
             'perl')
groups=('raku')
url='https://github.com/perl6/nqp'
license=('PerlArtistic')
source=("https://github.com/perl6/nqp/releases/download/$pkgver/$pkgname-$pkgver.tar.gz"{,.asc})
sha256sums=('4ccc9c194322c73f4c8ba681e277231479fcc2307642eeeb0f7caa149332965b'
            'SKIP')
validpgpkeys=('FE750D152426F3E50953176ADE8F8F5E97A8FCDE')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Building...'
  perl Configure.pl \
    --prefix='/usr' \
    --backends='moar'
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Installing...'
  make \
    DESTDIR="$pkgdir" \
    install
}
