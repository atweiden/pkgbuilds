# Maintainer: Andy Weidenbaum <archbaum@gmail.com>
# curl https://keybase.io/alexdaniel/pgp_keys.asc | gpg --import

pkgname=nqp
pkgver=2019.07.1
pkgrel=1
pkgdesc="Lightweight Perl6-like environment for virtual machines, with MoarVM support"
arch=('aarch64' 'armv6h' 'armv7h' 'i686' 'x86_64')
depends=('moarvm')
makedepends=('make' 'perl')
groups=('perl6')
url="https://github.com/perl6/nqp"
license=('PerlArtistic')
source=(https://github.com/perl6/nqp/releases/download/$pkgver/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('7324e5b84903c5063303a3c9fb6205bdf5f071c9778cc695ea746e7d41b12224'
            'SKIP')
validpgpkeys=('E1A575CCC2D58C7AA5609FE14710D9D434A4A3C6')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Building...'
  perl Configure.pl --prefix=/usr --backends=moar
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Installing...'
  make DESTDIR="$pkgdir" install
}
