# Maintainer: Andy Weidenbaum <atweiden@tutanota.de>

pkgname='nqp'
pkgver=2020.05
pkgrel=1
pkgdesc='Lightweight Raku-like environment for virtual machines'
arch=('aarch64'
      'armv6h'
      'armv7h'
      'i686'
      'x86_64')
depends=('libatomic_ops'
         'libffi'
         'libtommath'
         'libuv'
         'moarvm')
makedepends=('git'
             'make'
             'perl')
groups=('raku')
url='https://github.com/Raku/nqp'
license=('PerlArtistic')
source=("https://rakudo.org/dl/nqp/$pkgname-$pkgver.tar.gz"{,.asc})
sha256sums=('291b92d9db968a691195adb1c9533edc1076d12d6617d6d931e40595e906b577'
            'SKIP')
validpgpkeys=('FE750D152426F3E50953176ADE8F8F5E97A8FCDE')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Building...'
  perl Configure.pl \
    --prefix='/usr' \
    --backends='moar'
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Installing...'
  make \
    DESTDIR="$pkgdir" \
    install
}
