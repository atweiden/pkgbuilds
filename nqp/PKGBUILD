# Maintainer: Andy Weidenbaum <atweiden@tutanota.de>

pkgname='nqp'
pkgver=2022.06
pkgrel=1
pkgdesc='Lightweight Raku-like environment for virtual machines'
arch=('aarch64'
      'armv6h'
      'armv7h'
      'i686'
      'x86_64')
depends=('libatomic_ops'
         'libffi'
         'libtommath'
         'libuv'
         'moarvm')
makedepends=('git'
             'make'
             'perl')
groups=('raku')
url='https://github.com/Raku/nqp'
license=('PerlArtistic')
source=("https://rakudo.org/dl/nqp/$pkgname-$pkgver.tar.gz"{,.asc})
sha256sums=('8edf620e5994b265578a037eebdca0e3d622f993ef89a4195553fa01f81e4f24'
            'SKIP')
# https://keys.openpgp.org/vks/v1/by-fingerprint/59E634736AFDCF9C6DBAC382602D51EACA887C01
validpgpkeys=('59E634736AFDCF9C6DBAC382602D51EACA887C01')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Building...'
  perl Configure.pl \
    --prefix='/usr' \
    --backends='moar'
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Installing...'
  make \
    DESTDIR="$pkgdir" \
    install
}
