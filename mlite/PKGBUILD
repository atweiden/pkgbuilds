# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=mlite
pkgver=20141229
pkgrel=1
pkgdesc="Functional programming language combining the dynamic nature of Scheme and Standard ML with advanced guarded pattern matching"
arch=('i686' 'x86_64')
makedepends=('groff' 'make')
url="http://t3x.org/mlite"
license=('PublicDomain')
source=(http://t3x.org/mlite/mlite-$pkgver.tgz)
sha256sums=('54c24e6703baded5076edefb206985a62ad28c6f9575ed5394cf82284934c65a')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Building...'
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Testing...'
  make test
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Installing documentation...'
  install -Dm 644 ADDENDA_AND_ERRATA CHANGES README Todo mlite.txt mll.pdf \
          -t "$pkgdir/usr/share/doc/$pkgname"

  msg2 'Installing...'
  make PREFIX="$pkgdir/usr" install
}
