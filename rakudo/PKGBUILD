# Maintainer: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: spider-mario <spidermario@free.fr>
# curl https://keybase.io/alexdaniel/pgp_keys.asc | gpg --import

pkgname=rakudo
pkgver=2018.05
pkgrel=1
pkgdesc="Perl6 on MoarVM"
arch=('i686' 'x86_64')
depends=('moarvm' 'nqp')
makedepends=('make' 'perl')
optdepends=('gdb: GDB runner'
            'valgrind: Valgrind runner')
groups=('perl6')
url="https://github.com/rakudo/rakudo"
license=('PerlArtistic')
source=(https://rakudo.perl6.org/downloads/$pkgname/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('7d8479fd35aa5fc44d4098d69d1233441c6f0fe845327c5697ad68e2e54fdd47'
            'SKIP')
validpgpkeys=('E1A575CCC2D58C7AA5609FE14710D9D434A4A3C6')
provides=('perl6' 'rakudo')
conflicts=('perl6' 'rakudo')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Building...'
  perl Configure.pl --prefix=/usr --backends=moar
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Installing...'
  export RAKUDO_LOG_PRECOMP=1
  export RAKUDO_RERESOLVE_DEPENDENCIES=0
  make DESTDIR="$pkgdir" install
  install -Dm 755 tools/install-dist.pl "$pkgdir/usr/bin/perl6-install-dist"
}
