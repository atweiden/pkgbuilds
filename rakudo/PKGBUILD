# Maintainer: Andy Weidenbaum <atweiden@tutanota.de>

pkgname='rakudo'
pkgver=2022.06
pkgrel=1
pkgdesc='Mature, production-ready implementation of the Raku language'
arch=('aarch64'
      'armv6h'
      'armv7h'
      'i686'
      'x86_64')
depends=('libatomic_ops'
         'libffi'
         'libtommath'
         'libuv'
         'moarvm'
         'nqp'
         'zstd')
makedepends=('git'
             'make'
             'perl')
optdepends=('gdb: GDB runner'
            'valgrind: Valgrind runner')
groups=('raku')
url='https://github.com/rakudo/rakudo'
license=('PerlArtistic')
source=("https://rakudo.org/dl/rakudo/$pkgname-$pkgver.tar.gz"{,.asc})
sha256sums=('fc31b3bfb006e70df918c6f0cea110d1e3d354c3dbe6d86ad75dc60f6090c0e8'
            'SKIP')
# https://keys.openpgp.org/vks/v1/by-fingerprint/59E634736AFDCF9C6DBAC382602D51EACA887C01
validpgpkeys=('59E634736AFDCF9C6DBAC382602D51EACA887C01')
provides=('raku')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Building...'
  perl Configure.pl \
    --prefix='/usr' \
    --backends='moar'
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 'Installing...'
  export RAKUDO_LOG_PRECOMP=1
  export RAKUDO_RERESOLVE_DEPENDENCIES=0
  make \
    DESTDIR="$pkgdir" \
    install
  install -Dm 755 tools/install-dist.p6 \
    "$pkgdir/usr/bin/raku-install-dist"
}
